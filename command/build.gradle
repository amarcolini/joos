plugins {
    id 'maven-publish'
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

repositories {
    google()
    maven { url = 'https://maven.brott.dev/' }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events("passed", "skipped", "failed")
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    implementation "org.apache.commons:commons-math3:3.6.1"
    compileOnly "org.firstinspires.ftc:RobotCore:7.0.0"
    compileOnly 'org.firstinspires.ftc:Hardware:7.0.0'
    api 'com.acmerobotics.dashboard:dashboard:0.4.4'
    api project(":navigation")
    implementation project(":command:annotation")

    testImplementation "org.junit.jupiter:junit-jupiter-api:5.7.2"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.7.2"

    testImplementation "org.knowm.xchart:xchart:3.8.1"
}

tasks["clean"].doFirst {
    delete file("graphs")
}

android {
    compileSdkVersion 30
    compileOptions {
        sourceCompatibility "1.8"
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }
    defaultConfig {
        minSdkVersion 23
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "sources"
}

project.afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifactId = "command"
                artifact sourcesJar
                pom {
                    name = "Joos"
                    description = "A comprehensive kotlin library designed for FTC."
                    url = 'https://github.com/amarcolini/joos'
                    developers {
                        developer {
                            id = 'amarcolini'
                            name = 'Alessandro Marcolini'
                        }
                    }
                }
            }
        }

        repositories {
            maven {
                name = 'testRepo'
                url = '../testRepo'
            }
        }
    }
}